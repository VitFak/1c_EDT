Функция ПолучитьДинамическуюЦену(Дата,Номенклатура,Показатель) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаНоменклатура.Ссылка КАК Номенклатура,
	|	ДинамическиеЦеныНоменклатурыСрезПоследних.Цена,
	|	ДинамическиеЦеныНоменклатурыСрезПоследних.Валюта
	|ПОМЕСТИТЬ ВТЦены
	|ИЗ
	|	Справочник.Номенклатура КАК ТаблицаНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДинамическиеЦеныНоменклатуры.СрезПоследних(&ТекДата, ) КАК ДинамическиеЦеныНоменклатурыСрезПоследних
	|		ПО ТаблицаНоменклатура.Ссылка = ДинамическиеЦеныНоменклатурыСрезПоследних.Номенклатура
	|ГДЕ
	|	ТаблицаНоменклатура.Ссылка = &Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТЦены.Номенклатура,
	|	ПорогиДинамическихЦенПороги.Цена,
	|	ВТЦены.Валюта,
	|	КурсыВалютСрезПоследних.Курс,
	|	КурсыВалютСрезПоследних.Кратность
	|ИЗ
	|	ВТЦены КАК ВТЦены
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПорогиДинамическихЦен.Пороги КАК ПорогиДинамическихЦенПороги
	|		ПО ВТЦены.Цена = ПорогиДинамическихЦенПороги.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ТекДата, ) КАК КурсыВалютСрезПоследних
	|		ПО ВТЦены.Валюта = КурсыВалютСрезПоследних.Валюта
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ПорогиДинамическихЦенПороги.До = 0
	|				ТОГДА &х > ПорогиДинамическихЦенПороги.От
	|			ИНАЧЕ &х > ПорогиДинамическихЦенПороги.От
	|					И &х < ПорогиДинамическихЦенПороги.До
	|		КОНЕЦ";
	
	Запрос.УстановитьПараметр("Номенклатура",Номенклатура);
	Запрос.УстановитьПараметр("ТекДата",КонецДня(Дата));
	Запрос.УстановитьПараметр("х",Показатель);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Цена = РаботаСКурсамиВалютКлиентСервер.ПересчитатьПоКурсу(
		Выборка.Цена, 
		Новый Структура("Валюта, Курс, Кратность", Выборка.Валюта, Выборка.Курс, Выборка.Кратность),
		Новый Структура("Валюта, Курс, Кратность", Константы.ВалютаРегламентированногоУчета.Получить(), 1, 1));
		
		Возврат Цена;
	Иначе
		Возврат 0;
	КонецЕсли; 
	
	
КонецФункции  


Функция ПолучитьЦену(Номенклатура,Спецификация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЦеныНоменклатурыСрезПоследних.Цена,
	|	ЦеныНоменклатурыСрезПоследних.Валюта,
	|	КурсыВалютСрезПоследних.Курс,
	|	КурсыВалютСрезПоследних.Кратность
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|			&ТекДата,
	|			Номенклатура = &Номенклатура
	|				И Спецификация = &Спецификация) КАК ЦеныНоменклатурыСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ТекДата, ) КАК КурсыВалютСрезПоследних
	|		ПО ЦеныНоменклатурыСрезПоследних.Валюта = КурсыВалютСрезПоследних.Валюта";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Спецификация", Спецификация);
	Запрос.УстановитьПараметр("ТекДата", ТекущаяДата());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Цена = РаботаСКурсамиВалютКлиентСервер.ПересчитатьПоКурсу(
		Выборка.Цена, 
		Новый Структура("Валюта, Курс, Кратность", Выборка.Валюта, Выборка.Курс, Выборка.Кратность),
		Новый Структура("Валюта, Курс, Кратность", Константы.ВалютаРегламентированногоУчета.Получить(), 1, 1));
		
		Возврат Цена;
	Иначе
		Возврат 0;
	КонецЕсли; 
	
КонецФункции

